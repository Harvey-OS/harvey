//
// usage: acid -l pool -l leak
//
include("/sys/src/libc/port/pool.acid");

defn
dumppool(p)
{
	complex Pool p;
	a = p.arenalist;

	while a != 0 && a < 0x60000000 do {
		complex Arena a;
		dumparena(a);
		a = a.down;
	}
}

defn
dumparena(arena)
{
	local atail, b, nb;

	atail = A2TB(arena);
	complex Bhdr arena;
	b = a;
	while b < atail && b.magic != ARENATAIL_MAGIC do {
		dumpblock(b);
		nb = B2NB(b);
		if nb == b then {
			print("B2NB(", b\X, ") = b\n");
			b = atail;	// end loop
		}
		if nb > atail then {
			b = (Bhdr)(b+4);
			print("lost at block ", (b-4)\X, ", scanning forward\n");
			while b < atail && b.magic != ALLOC_MAGIC && b.magic != FREE_MAGIC do
				b = (Bhdr)(b+4);
			print("stopped at ", b\X, " ", *b\X, "\n");
		}else
			b = nb;
	}
	if b != atail then
		print("found wrong tail to arena ", arena\X, " wanted ", atail\X, "\n");
}

defn
isptr(a)
{
	if end <= a && a < xbloc then
		return 1;
	if 0x7efff000 <= a && a < 0x7ffff000 then
		return 1;
	return 0;
}

defn
dumpblock(addr)
{
	complex Bhdr addr;

	if addr.magic == ALLOC_MAGIC || addr.magic == FREE_MAGIC then {
		local a, x, s;

		a = addr;
		complex Alloc a;

		x = addr+8;
		if addr.magic == ALLOC_MAGIC then
			s = "block";
		else
			s = "free";
		print(s, " ", addr\X, " ", a.size\X, " ");
		print(*(addr+8)\X, " ", *(addr+12)\X, "\n");
	}
}

defn
dumprange(s, e, type)
{
	local x, y;

	print("range ", type, " ", s\X, " ", e\X, "\n");
	x = s;
	while x < e do {
		y = *(x\X);
		if isptr(y) then print("data ", x\X, " ", y\X, " ", type, "\n");
		x = x + 4;
	}
}

defn
stacktop()
{
	local e, m;
	
	m = map();
	while m != {} do {
		e = head m;
		if e[0] == "*data" then
			return e[2];
		m = tail m;
	}
	return 0x7ffff000;
}
			
defn
dumpmem()
{
	local s, top;

	xbloc = *bloc;
	// assume map()[1] is "data" 
	dumprange(map()[1][1], end, "bss");	// bss
	dumprange(end, xbloc, "alloc");	// allocated

	top = stacktop() - 8;
	if top-0x01000000 < *SP && *SP < top then
		s = *SP;
	else
		s = top-32*1024;

	dumprange(s, top, "stack");
}

defn
dumpregs()
{
	dumprange(0, sizeofUreg, "reg");
}


defn
leakdump(l)
{
	print("==LEAK BEGIN==\n");
	dumppool(sbrkmem);
	dumpmem();
	dumpregs();
	while l != {} do {
		setproc(head l);
		dumpregs();
		l = tail l;
	}
	print("==LEAK END==\n");
}

defn
blockdump()
{
	print("==BLOCK BEGIN==\n");
	dumppool(sbrkmem);
	print("==BLOCK END==\n");
}
