#!/bin/rc

awk '
BEGIN{		collect = isdev = isbootdir = 0;
}

/^$/{		next;
}
/^#/{		next;
}
collect && /^[^	\t]/{
		collect = isdev = isbootdir = 0;
}
collect && $0 ~ /[^ \t]+/{
		if(isbootdir){
			x = $1;
			gsub(/[^a-zA-Z0-9_]/, "_", x);
			obj[x ".root.'$O'"]++;
			next;
		}
		if(isdev)
			obj["dev" $1 "'.$O'"]++;
		else
			obj[$1 "'.$O'"]++;
		for(i = 2; i <= NF; i++){
			if($i !~ "[+=-].*")
				obj[$i "'.$O'"]++;
		}
}
$0 ~ /^[^ \t]/{
		if($1 ~ "dev"){
			isdev = 1;
			collect = 1;
		}
		else if($1 ~ "misc" || $1 ~ "link" || $1 ~ "ip")
			collect = 1;
		else if($1 ~ "bootdir"){
			isbootdir = 1;
			collect = 1;
		}
		next
}

END{
		x = ""
		for(i in obj)
			x = x i "\n"
		printf x
}' $*
